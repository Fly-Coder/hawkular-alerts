/*
 * Copyright 2015 Red Hat, Inc. and/or its affiliates
 * and other contributors as indicated by the @author tags.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
package org.hawkular.alerts.engine.rules

import org.hawkular.alerts.api.model.condition.Alert;
import org.hawkular.alerts.api.model.condition.AvailabilityCondition;
import org.hawkular.alerts.api.model.condition.AvailabilityConditionEval;
import org.hawkular.alerts.api.model.condition.ConditionEval;
import org.hawkular.alerts.api.model.condition.CompareCondition;
import org.hawkular.alerts.api.model.condition.CompareConditionEval;
import org.hawkular.alerts.api.model.condition.ExternalCondition;
import org.hawkular.alerts.api.model.condition.ExternalConditionEval;
import org.hawkular.alerts.api.model.condition.StringCondition;
import org.hawkular.alerts.api.model.condition.StringConditionEval;
import org.hawkular.alerts.api.model.condition.ThresholdCondition;
import org.hawkular.alerts.api.model.condition.ThresholdConditionEval;
import org.hawkular.alerts.api.model.condition.ThresholdRangeCondition;
import org.hawkular.alerts.api.model.condition.ThresholdRangeConditionEval;
import org.hawkular.alerts.api.model.dampening.Dampening;
import org.hawkular.alerts.api.model.dampening.Dampening.Type;
import org.hawkular.alerts.api.model.data.Availability;
import org.hawkular.alerts.api.model.data.Availability.AvailabilityType
import org.hawkular.alerts.api.model.data.NumericData;
import org.hawkular.alerts.api.model.data.StringData;
import org.hawkular.alerts.api.model.action.Action;
import org.hawkular.alerts.api.model.trigger.Trigger;
import org.hawkular.alerts.api.model.trigger.Trigger.Mode;
import org.hawkular.alerts.api.model.trigger.TriggerTemplate.Match;
import org.hawkular.alerts.api.services.ActionsService;

import org.jboss.logging.Logger;

import java.util.Set;
import java.util.List;
import java.util.Map;

global Logger log;
global ActionsService actions;
global List alerts;
global Set pendingTimeouts;
global Map autoResolvedTriggers;
global Set disabledTriggers;


////// CONDITION MATCHING
//
// To perform dampening it is important not only to record positive condition evaluations but also false
// evaluations.  True evals will contribute to an alert firing. And for default dampening (1 consecutive condition set
// match) it is enough to fire the trigger.  But negative evals also contribute to dampening, resetting strict
// dampening, and potentially resetting relaxed dampening.  So, each defined Condition will be evaluated when its
// relevant Data facts are present in working memory , generating a ConditionEval fact which will then be applied
// to the relevant Dampening fact.
//
// Data facts will be applied to all relevant Condition evals, and then will be retracted (using rule complexity
// to fire the retract rule after the condition evals).
//
// It is a precondition that all Triggers in working memory are enabled (i.e. enabled == true ).

// ConditionEval rules

rule Threshold
    when 
        $t : Trigger( $tid : id, $tmode : mode )
        $c : ThresholdCondition ( triggerId == $tid, triggerMode == $tmode, $did : dataId )
        $d : NumericData( $did == id )
    then
        ThresholdConditionEval ce = new ThresholdConditionEval($c, $d);
        if (log != null && log.isDebugEnabled()) {
            log.debug("Threshold Eval: " + (ce.isMatch() ? "  Match! " : "no match ")  + ce.getLog());
        }
        insert( ce );
end

rule ThresholdRange
    when 
        $t : Trigger( $tid : id, $tmode : mode )
        $c : ThresholdRangeCondition ( triggerId == $tid, triggerMode == $tmode, $did : dataId )
        $d : NumericData( $did == id )
    then
        ThresholdRangeConditionEval ce = new ThresholdRangeConditionEval($c, $d);
        if (log != null && log.isDebugEnabled()) {
            log.debug("ThresholdRange Eval: " + (ce.isMatch() ? "  Match! " : "no match ")  + ce.getLog());
        }
        insert( ce );
end

// Note that at the moment a CompareCondition will only ever match if the NumericData facts are inserted
// at the same time. If only one Data is present in WM it will not match this rule and will get retracted
// as unused/unnecessary.  We may have to revisit this behavior and nail down the desired semantics of this
// kind of condition. But this may be correct behavior as is.  It makes sense to me that this sort of condition
// should likely be used on data reported in the same batch. But if not, one approach may be to insert a special
// fact type, here, like CompareData (in addition to inserting as NumericData, for other condition evals) and then
// handle that retraction differently, always holding onto the most recent data for the dataId, as needed for comparing
// data when it arrives at different times.
rule Compare
    when 
        $t  : Trigger( $tid : id, $tmode : mode )
        $c  : CompareCondition ( triggerId == $tid, triggerMode == $tmode, $d1id : dataId, $d2id : data2Id )
        $d1 : NumericData( $d1id == id )
        $d2 : NumericData( $d2id == id )
    then
        CompareConditionEval ce = new CompareConditionEval($c, $d1, $d2);
        if (log != null && log.isDebugEnabled()) {
            log.debug("Compare Eval: " + (ce.isMatch() ? "  Match! " : "no match ")  + ce.getLog());
        }
        insert( ce );
end

rule Availability
    when 
        $t : Trigger( $tid : id, $tmode : mode )
        $c : AvailabilityCondition ( triggerId == $tid, triggerMode == $tmode, $did : dataId )
        $d : Availability( $did == id )
    then
        AvailabilityConditionEval ce = new AvailabilityConditionEval($c, $d);
        if (log != null && log.isDebugEnabled()) {
            log.debug("Availability Eval: " + (ce.isMatch() ? "  Match! " : "no match ")  + ce.getLog());
        }
        insert( ce );
end

rule String
    when 
        $t : Trigger( $tid : id, $tmode : mode )
        $c : StringCondition ( triggerId == $tid, triggerMode == $tmode, $did : dataId  )
        $d : StringData( $did == id )
    then
        StringConditionEval ce = new StringConditionEval($c, $d);
        if (log != null && log.isDebugEnabled()) {
            log.debug("String Eval: " + (ce.isMatch() ? "  Match! " : "no match ")  + ce.getLog());
        }
        insert( ce );
end

rule External
    when 
        $t : Trigger( $tid : id, $tmode : mode )
        $c : ExternalCondition ( triggerId == $tid, triggerMode == $tmode, $did : dataId  )
        $d : StringData( $did == id )
    then
        ExternalConditionEval ce = new ExternalConditionEval($c, $d);
        if (log != null && log.isDebugEnabled()) {
            log.debug("External Eval: " + (ce.isMatch() ? "  Match! " : "no match ")  + ce.getLog());
        }
        insert( ce );
end

// Data retraction rules
// These rules are expected to fire after any Eval rules, due to their simplicity.  Note that Data is not retracted
// in the (above) rules, that generate ConditionEval facts, because one Datum can generate multiple evals, for different
// Triggers.

rule RetractProcessedNumericData
    when
        $d : NumericData()
    then
        if (log != null && log.isDebugEnabled()) {
            log.debug( "Retracting " + $d + "..." );
        }
        retract ( $d );
end

rule RetractProcessedStringData
    when
        $d : StringData()
    then
        if (log != null && log.isDebugEnabled()) {
            log.debug( "Retracting " + $d + "..." );
        }
        retract ( $d );
end

rule RetractProcessedAvailabilityData
    when
        $d : Availability()
    then
        if (log != null && log.isDebugEnabled()) {
            log.debug( "Retracting " + $d + "..." );
        }
        retract ( $d );
end

////// DAMPENING
//
// Dampening defines for each trigger how the number of positive, negative, and total evaluations determine
// whether a trigger fires.  Additionally, there may be a constraint on the period of time involved in the
// evaluations.
//
// Dampening is optionally defined for a Trigger.  If not defined then default dampening will be applied for uniformity
// of processing.  Default dampening is basically handled as STRICT( 1 ).  Meaning 1 positive, consecutive evaluation
// is required for the trigger to fire.  In other words, if the conditions match, the trigger fires.
//
// So, there is one Dampening fact for each Trigger fact.  And it is continually updated given each relevant condition
// set evaluation for the trigger.
//
// The Dampening fact is updated on each condition evaluation and then the ConditionEvaluation fact is retracted
// from working memory.  For single-condition triggers it is fairly straightforward; each condition evaluation results
// in a dampening evaluation.
//
// Understanding multi-condition Trigger evaluation is important.  ConditionEvals are generated when the relevant Data
// appears in working memory.  Data for specific DataIds can appear at different rates. There can be several more
// ConditionEvals for DataId X than for DataId Y, or even before we have a single eval for DataId Y.  Our approach is
// chosen for two reasons: simplicity of understanding, and the general desire for recency in alerting.  For 
// *multi-condition* Trigger evaluations we test only tuples containing the most recent evaluation of each condition.
//
// For example, consider an ALL-match Trigger T with two conditions, (X > 100) and (Y > 200), and dampening Strict(2).
// Now assume Data arrives like this:
//     t1, X=125
//     t2, X=50
//     t3, Y=300
//     t4, X=110
//     t5, Y=150
// The t1 condition eval of X=125 will be superseded by the t2 condition eval of X=50. When Y is finally reported at t3,
// the tuple tested for T is (X=50, Y=300). The dampening eval is false because the X condition (50 > 100) is false.
// At t4 we test the tuple (X=110, Y=300). The dampening eval is true because both conditions are met.  T does not
// fire because we need two consecutive true tuples. At t5 the condition eval of Y=300 will be superseded by
// Y=150, the tuple (X=110, Y=150) will evaluate to false.  T will not fire, the dampening will reset.
//
// Now assume T were an ANY-match trigger. For ANY-match we still use a tuple with the most recent evaluation for each
// condition. But, we don't need an evaluation for every condition, and we only need one condition eval to be true in
// order to satisfy the T.  The t1 evaluation of (x=125, Y=N/A) is true because the X condition is true. We can ignore
// the Y condition.  T does not fire because we need two consecutive true tuples.  The t2 evaluation of (x=50, Y=N/A)
// is false. T does not fire, the dampening is reset.  The t3 evaluation of (x=50, Y=300) is true because the Y
// condition is true. T does not fire because we need two consecutive true tuples.  The t4 evaluation of (x=110, Y=300)
// is true in both ways.  T fires and the dampening is reset. The t5 evaluation of (x=110, Y=150) is true because the
// X condtion is again true, and so on...
//
// Given the above approach to matching, we must hold onto the most recent evaluation of each condition used in
// a multi-condition Trigger.  What is important to understand is the most recent evaluation of each condition
// is held inside the relevant Dampening record and not as a Fact in working memory.  That allows us to have very
// simple processing here in the rules.  We just take every condition evaluation, have the required dampening
// Fact process it, and then retract the ConditionEvaluation.
//

// Dampening update rules

rule ProvideDefaultDampening
    when
        $t : Trigger( $tid : id, $tmode : mode )
        not Dampening( triggerId == $tid, triggerMode == $tmode )
    then
        if (log != null && log.isDebugEnabled()) {
            log.debug("Adding default " + $tmode + " dampening for trigger! " + $t.getId());
        }
        Dampening d = Dampening.forStrict( $tid, $tmode, 1 );
        insert( d );
end

rule DampenTrigger
    when
        $t  : Trigger( $tid : id, $tmode : mode )
        $d  : Dampening( triggerId == $tid, triggerMode == $tmode, satisfied == false )
        $ce : ConditionEval ( triggerId == $tid )
    then
        retract( $d );
        retract ( $ce );

        $d.perform( $t.getMatch(), $ce );

        insert( $d );

        if (log != null && log.isDebugEnabled()) {
            log.debugf( "Updated %s using [match=%s] %s", $d, $t.getMatch(), $d.getCurrentEvals().values() );
            log.debugf( "Retracted %s", $ce );
        }
end


// Dampening with STRICT_TIMEOUT
// Because we are not running the engine in Stream/CEP mode and instead use discrete rulebase executions, we
// can not perform temporal reasoning inside the rules.  So, to perform timeouts we "post" STRICT_TIMEOUT
// Dampening records when the timer should start, so that the timeouts  can be checked externally.  We remove
// timeouts when the dampening is reset.
rule DampeningTimeoutAdd
    when
        $d   : Dampening( type == Type.STRICT_TIMEOUT, numTrueEvals == 1, satisfied == false )
    then
        pendingTimeouts.add( $d );
end 

// TODO: does this rule fire too often? It may fire each time the condition set is evaluates to false. Although,
//       it is fairly lightweight. 
rule DampeningTimeoutRemove
    when
        $d   : Dampening( type == Type.STRICT_TIMEOUT, numTrueEvals == 0, satisfied == false )
    then
        pendingTimeouts.remove( $d );
end 



////// ALERT GENERATION / TRIGGER MODE HANDLING

// If a Trigger's FIRE mode Dampening is satisfied, then the Trigger fires and generates an Alert.  The Trigger's
// FIRE mode Dampening fact is then reset and updated in working memory, ready to again track evals for the Trigger.
// If the Trigger has safety mode enabled then we toggle the Trigger to SAFETY mode and it can not fire again
// until the safety mode Dampening is satisfied and the Trigger returns to FIRE mode.
rule AlertOnSatisfiedDampening
    when
        $t  : Trigger( mode == Mode.FIRING, $tid : id )
        $d  : Dampening( triggerMode == Mode.FIRING, triggerId == $tid, satisfied == true )
    then
        if (log != null && log.isDebugEnabled()) {
            log.debug("AlertOnSatisfiedDampening! " + $d.log());
        }

        Alert newAlert = new Alert( $t.getTenantId(), $tid, $t.getSeverity(), $d.getSatisfyingEvals() );
        newAlert.setTrigger($t);
        newAlert.setDampening($d);
        newAlert.setContext($t.getContext());
        alerts.add(newAlert);
        if (actions != null) {
            for (String actionPlugin : $t.getActions().keySet()) {
                for (String actionId : $t.getActions().get(actionPlugin)) {
                    Action action = new Action($t.getTenantId(), actionPlugin, actionId, newAlert);
                    actions.send(action);
                }
            }
        }

        retract( $d );
        $d.reset();
        insert( $d );

        if ($t.isAutoResolve()) {
            if (log != null && log.isDebugEnabled()) {
                log.debug("Setting Trigger to AutoResolve Mode! " + $t);
            }
            retract( $t );
            $t.setMode(Mode.AUTORESOLVE);
            insert( $t );

        } else if ($t.isAutoDisable()) {
            if (log != null && log.isDebugEnabled()) {
                log.debug("Setting Trigger Disabled! " + $t);
            }

            // the autoDisable trigger handling will result in a trigger reload, so we can just retract the
            // current definition. And given that it will be disabled, it actually will not get loaded.
            disabledTriggers.add( $t );

            retract( $t );

        } else if (log != null && log.isDebugEnabled()) {
            log.debug("Trigger remains in Firing mode, AutoDisable and AutoResolve not set. " + $t);
        }
end


rule SetFiringModeOnSatisfiedDampening
    when
        $t  : Trigger( mode == Mode.AUTORESOLVE, $tid : id )
        $d  : Dampening( triggerMode == Mode.AUTORESOLVE, triggerId == $tid, satisfied == true )
    then
        if (log != null && log.isDebugEnabled()) {
            log.debug("SetFiringModeOnSatisfiedDampening! " + $d.log());
        }

        // the autoResolved trigger handling will result in a trigger reload, so we can just retract the
        // current definition. We set it to FIRING mode but that is mainly a cosmetic change and affects
        // only the Trigger instance placed into autoResolvedTriggers.
        $t.setMode(Mode.FIRING);
        autoResolvedTriggers.put( $t, $d.getSatisfyingEvals() );

        retract( $d );
        retract( $t );
end
